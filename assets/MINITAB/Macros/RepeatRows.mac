macro
repeatrows col.1-col.ncols num

#This macro will accept a column (or columns) and will overwrite it
#with a new column where each original row is repeated num times in
#succession. For example, if the command:
#   mtb> %repeatrows c1 3
#is executed and c1 initially contains {2,8,5}, after the macro call
#c1 will contain {2,2,2,8,8,8,5,5,5}. This command is particularly
#useful to replicate a designed experiment if you don't want to stack
#the replicates.

#WARNING: YOUR ORIGINAL COLUMN WILL BE OVERWRITTEN!!!

#Copyright © Mathews Malnar and Bailey, Inc., 3 May 2002

mcolumn col.1-col.ncols dest.1-dest.ncols
mconstant num i j k L nrows

count col.1 nrows

do i=1:ncols			#the column
   do j=1:nrows			#the row of the original data set
      do k=1:num		#this counts off the replicated rows
         let L=(j-1)*num+k	#row index for the destination column
         let dest.i(L)=col.i(j)	#write the destination column values
      enddo
   enddo
   let col.i=dest.i		#overwrite the original column
enddo

endmacro


